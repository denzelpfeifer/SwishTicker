apply plugin: 'com.android.application'

android {
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "swishticker-v${variant.versionName}.apk"
        }
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.hinterlong.kevin.swishticker"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'basketballstats'
            keyAlias 'basketballstats'
            keyPassword 'basketballstats'
        }
        if (file('upload.keystore').exists()) {
            upload {
                storeFile file('upload.keystore')
                storePassword System.getenv('BASKETBALLSTATS_UPLOAD_STORE_PASSWORD')
                keyAlias 'basketballstats'
                keyPassword System.getenv('BASKETBALLSTATS_UPLOAD_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            if (file('upload.keystore').exists()) {
                signingConfig signingConfigs.upload
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        lintConfig rootProject.file('lint.xml')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    final support_library_version = '27.0.2'
    implementation "com.android.support:appcompat-v7:$support_library_version"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
}
